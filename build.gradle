buildscript {
    ext {
        springBootVersion = '1.5.8.RELEASE'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'idea'

apply plugin: 'org.springframework.boot'
apply plugin: 'docker'

group = 'xyz.lomasz'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter'

    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-data-rest"

    compile "com.h2database:h2:1.4.196"

    compile "io.springfox:springfox-swagger2:2.7.0"
    compile "io.springfox:springfox-swagger-ui:2.7.0"

    compileOnly "org.projectlombok:lombok:1.16.18"

    compile "org.elasticsearch.client:elasticsearch-rest-client:6.1.1"
    compile "org.elasticsearch.client:elasticsearch-rest-high-level-client:6.1.1"
    compile "org.elasticsearch:elasticsearch:6.1.1"

    compile "org.apache.commons:commons-lang3:3.7"

    //TEST
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "io.rest-assured:rest-assured:3.0.6"
    testCompile "org.assertj:assertj-core:3.8.0"

}

apply plugin: "jacoco"

jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

test.finalizedBy jacocoTestReport

task buildDocker(type: Docker) {
    baseImage = 'develar/java:latest'
    push = project.hasProperty('push')
    tag = 'lomasz-app'
    addFile {
        from jar
        rename { 'app.jar' }

    }
    entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'])
    exposePort(666)
}

buildDocker.dependsOn(build)
